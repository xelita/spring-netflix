buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${gradleSpringPluginVersion}")
        classpath("se.transmode.gradle:gradle-docker:${gradleDockerPluginVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'docker'

ext {
    name = theName
    description = theDescription
    version = theVersion
}

jar {
    baseName = theName
    version = theVersion
}

group = "xelita"

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release/' }
    maven { url "http://repo.spring.io/libs-snapshot-local" }
    maven { url "http://repo.spring.io/libs-milestone-local" }
    maven { url "http://repo.spring.io/libs-release-local" }
}

sourceCompatibility = theSourceCompatibility
targetCompatibility = theSourceCompatibility

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        mavenBom("org.springframework.cloud:spring-cloud-starter-parent:${springCloudVersion}")
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.cloud:spring-cloud-starter-eureka-server'
}

task wrapper(type: Wrapper) {
    gradleVersion = wrapperVersion
}

